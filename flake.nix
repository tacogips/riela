{

  description = "riela Swift development environment";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/release-24.11";
    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs =
    {
      self,
      nixpkgs,
      nixpkgs-unstable,
      flake-utils,
      git-hooks,
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs { inherit system; };
        pkgs-unstable = import nixpkgs-unstable { inherit system; };
        lib = pkgs.lib;
        xcodeToolchain =
          let
            developerDir = "/Applications/Xcode.app/Contents/Developer";
          in
          {
            inherit developerDir;
            toolchainIdentifier = "com.apple.dt.toolchain.XcodeDefault";
            sdkRoot = "${developerDir}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX.sdk";
            toolchainBin = "${developerDir}/Toolchains/XcodeDefault.xctoolchain/usr/bin";
          };
        runtimePackages =
          with pkgs;
          [
            # Bun runtime for legacy verification scripts kept outside the Swift product.
            pkgs-unstable.bun

            # TypeScript tooling for legacy verification scripts kept outside the Swift product.
            pkgs-unstable.typescript
            pkgs-unstable.typescript-language-server

            # Rust-based JS/TS linter used by repository lint tasks.
            pkgs-unstable.biome

            # Development tools
            fd
            gnused
            gh
            go-task
            swiftlint

          ]
          ++ lib.optionals pkgs.stdenv.isLinux [
            podman
            podman-compose

          ];

        devOnlyPackages = with pkgs; [
          gitleaks
        ];

        preCommitCheck = git-hooks.lib.${system}.run {
          src = ./.;
          hooks = {
            gitleaks = {
              enable = true;
              name = "gitleaks";
              entry = "${pkgs.lib.getExe pkgs.gitleaks} git --pre-commit --redact --staged --verbose";
              language = "system";
              pass_filenames = false;
            };
          };
        };

        devPackages = runtimePackages ++ devOnlyPackages ++ preCommitCheck.enabledPackages;

      in
      {
        packages.dev-tools = pkgs.buildEnv {
          name = "riela-dev-tools";
          paths = devPackages;
          pathsToLink = [ "/bin" ];
        };

        checks.pre-commit-check = preCommitCheck;

        devShells.default = pkgs.mkShell {
          packages = devPackages;

          shellHook = ''
            # Dev-only: fixed root data dir for this checkout.
            export RIELA_ARTIFACT_DIR="$HOME/.riela/dev/riela-artifact"
            ${preCommitCheck.shellHook}
            ${lib.optionalString pkgs.stdenv.isDarwin ''
              export DEVELOPER_DIR="${xcodeToolchain.developerDir}"
              export SDKROOT="${xcodeToolchain.sdkRoot}"
              export TOOLCHAINS="${xcodeToolchain.toolchainIdentifier}"
              export PATH="${xcodeToolchain.toolchainBin}:$PATH"
            ''}

            echo "Riela Swift development environment ready"
            echo "Bun version: $(bun --version)"
            echo "TypeScript version: $(tsc --version)"
            echo "Biome version: $(biome --version 2>/dev/null || echo 'not available')"
            echo "Task version: $(task --version 2>/dev/null || echo 'not available')"
            echo "Gitleaks version: $(gitleaks version 2>/dev/null || echo 'not available')"
            ${lib.optionalString pkgs.stdenv.isLinux ''
              echo "Podman version: $(podman --version 2>/dev/null || echo 'not available')"
              echo "Podman Compose version: $(podman-compose --version 2>/dev/null || echo 'not available')"
            ''}
          '';
        };
      }
    );
}
